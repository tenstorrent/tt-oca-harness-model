//-----------------------------------------------------------------------------
// Efuse Program Interface
//
// Copyright 2025 Tenstorrent Inc.
//-----------------------------------------------------------------------------

module efuse_program_interface
#(
    parameter unsigned EFUSE_WORD_WIDTH = 32,

    parameter type efuse_addr_t = logic,
    parameter type efuse_data_t = logic,
    parameter type efuse_word_counter_t = logic,
    parameter type fuse_command_req_t = logic,
    parameter type fuse_command_resp_t = logic
) (
    input  logic                clk_i,
    input  logic                rst_ni,
    input  logic                test_en_i,

    input  logic                program_enable_i,
    output logic                is_programing_o,
    output efuse_addr_t         program_target_addr_o,

    input  efuse_addr_t         program_addr_i,
    input  logic                program_data_in_i,
    input  logic                program_go_i,
    input  logic                program_read_back_enable_i,

    output logic                program_busy_o,
    output logic                program_done_o,
    output logic                program_error_o,
    output efuse_data_t         program_read_back_data_o,

    input  logic                efuse_req_err_i,
    input  logic                secure_tm_blocked_i,

    input  logic                program_req_timeout_en_i,
    input  logic [27:0]         program_req_timeout_cycles_i,

    output fuse_command_req_t   fuse_command_req_o,
    input  fuse_command_resp_t  fuse_command_resp_i
);

    localparam fuse_command_req_t FUSE_COMMAND_REQ_DEFAULT = '0;

    // Timeout logic
    logic [27:0] timeout_count_q, timeout_count_d;

    fuse_command_req_t fuse_command_req_d, fuse_command_req_q;

    efuse_addr_t captured_program_addr;
    logic        captured_program_data;

    // Program execution state machine
    typedef enum logic {
        ST_PROGRAM_IDLE = 1'b0,
        ST_WAIT_RESP    = 1'b1
    } efuse_program_state_e;


    efuse_program_state_e program_state_q, program_state_d;
    assign is_programing_o = (program_state_q == ST_PROGRAM_IDLE) ? 1'b0 : 1'b1;
    assign program_target_addr_o = (program_state_q == ST_PROGRAM_IDLE) ? efuse_addr_t'(0) : captured_program_addr;

    // Capture program address and data when idle
    always_ff @(posedge clk_i or negedge rst_ni) begin
        if (!rst_ni) begin
            captured_program_addr <= 'd0;
            captured_program_data <= 'd0;
        end else if (program_state_q == ST_PROGRAM_IDLE) begin
            captured_program_addr <= program_addr_i;
            captured_program_data <= program_data_in_i;
        end
    end

    logic program_err_q, program_err_d;
    logic program_done_q, program_done_d;
    logic program_busy_q, program_busy_d;
    efuse_data_t program_read_back_data_q, program_read_back_data_d;

    always_comb begin

        timeout_count_d = timeout_count_q;

        program_state_d = program_state_q;
        program_err_d = program_err_q;
        program_done_d = program_done_q;
        program_busy_d = program_busy_q;
        program_read_back_data_d = program_read_back_data_q;
        fuse_command_req_d = fuse_command_req_q;

        unique case (program_state_q)
        ST_PROGRAM_IDLE: begin
            if (program_go_i) begin
                program_done_d = 1'b0;
                timeout_count_d = 'd0;
                if (!program_enable_i) begin
                    program_state_d = ST_PROGRAM_IDLE;
                    program_busy_d = 1'b0;
                    program_done_d = 1'b1;
                    program_err_d = 1'b1;
                end else if (program_data_in_i == 1'b0) begin
                    program_state_d = ST_PROGRAM_IDLE;
                    program_busy_d = 1'b0;
                    program_done_d = 1'b1;
                    program_err_d = 1'b1;
                end else begin
                    program_state_d = ST_WAIT_RESP;
                    program_busy_d = 1'b1;
                    fuse_command_req_d.address = program_addr_i;
                    fuse_command_req_d.write_data = program_data_in_i;
                    fuse_command_req_d.command = program_read_back_enable_i ? efuse_pkg::FUSE_COMMAND_WRITE_READ_BACK : efuse_pkg::FUSE_COMMAND_WRITE;
                    fuse_command_req_d.valid = 1'b1;
                    fuse_command_req_d.access_length_words = efuse_word_counter_t'(1);

                end
            end
        end

        ST_WAIT_RESP: begin
            // If req is blocked from a shadow reg lock, or physical efuse returned error, or secure_tm blocked it
            if (efuse_req_err_i || fuse_command_resp_i.status == 1'b1 || secure_tm_blocked_i) begin
                
                program_done_d = 1'b1;
                program_busy_d = 1'b0;
                program_err_d = 1'b1;
                fuse_command_req_d = FUSE_COMMAND_REQ_DEFAULT;
                program_state_d = ST_PROGRAM_IDLE;

            end else if (fuse_command_resp_i.valid) begin

                program_done_d = 1'b1;
                program_busy_d = 1'b0;
                program_err_d = fuse_command_resp_i.status;
                fuse_command_req_d = FUSE_COMMAND_REQ_DEFAULT;
                program_state_d = ST_PROGRAM_IDLE;

                program_read_back_data_d = fuse_command_resp_i.data;

            end else begin

                program_done_d = 1'b0;
                program_busy_d = 1'b1;
                program_err_d =  1'b0;

                if (program_req_timeout_en_i && timeout_count_d >= program_req_timeout_cycles_i) begin
                    program_done_d = 1'b1;
                    program_busy_d = 1'b0;
                    program_err_d = 1'b1;
                    fuse_command_req_d = FUSE_COMMAND_REQ_DEFAULT;
                    program_state_d = ST_PROGRAM_IDLE;
                end else if (program_req_timeout_en_i) begin
                    timeout_count_d = timeout_count_q + 1;
                end
            end

        end

        default: begin
            program_state_d = ST_PROGRAM_IDLE;
            program_busy_d = 1'b0;
            program_done_d = 1'b1;
            program_err_d = 1'b0;
            program_read_back_data_d = efuse_data_t'(0);
            fuse_command_req_d = FUSE_COMMAND_REQ_DEFAULT;
        end

        endcase
    end

    // Register the state
    always_ff @(posedge clk_i or negedge rst_ni) begin
        if (!rst_ni) begin
            program_state_q <= ST_PROGRAM_IDLE;
            fuse_command_req_q <= FUSE_COMMAND_REQ_DEFAULT;
            program_err_q <= 1'b0;
            program_done_q <= 1'b0;
            program_busy_q <= 1'b0;
            program_read_back_data_q <= efuse_data_t'(0);
            timeout_count_q <= 'd0;
        end else begin
            program_state_q <= program_state_d;
            fuse_command_req_q <= fuse_command_req_d;
            program_err_q <= program_err_d;
            program_done_q <= program_done_d;
            program_busy_q <= program_busy_d;
            program_read_back_data_q <= program_read_back_data_d;
            timeout_count_q <= timeout_count_d;
        end
    end

    assign program_busy_o = program_busy_q;
    assign program_done_o = program_done_q;
    assign program_error_o = program_err_q;
    assign program_read_back_data_o = program_read_back_data_q;
    assign fuse_command_req_o = fuse_command_req_q;

endmodule: efuse_program_interface
